plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '3.4.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.github.node-gradle.node' version '7.0.2'
}

group = 'com.intermancer.gaiaf.core'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

node {
    version = '22.12.0'
    npmVersion = '10.9.0'
    download = true
    workDir = file("${project.projectDir}/.gradle/nodejs")
    npmWorkDir = file("${project.projectDir}/.gradle/npm")
    nodeProjectDir = file("${project.projectDir}/frontend")
}

// Tell IntelliJ IDEA to treat frontend as excluded
idea {
    module {
        excludeDirs += file('frontend/node_modules')
        excludeDirs += file('frontend/dist')
        excludeDirs += file('frontend/.vite')

        // Mark frontend/src as a content root so IntelliJ understands the context
        sourceDirs += file('frontend/src')
    }
}

// Create a custom task for building the frontend
tasks.register('cleanFrontend', Delete) {
    delete file("${project.projectDir}/frontend/node_modules")
    delete file("${project.projectDir}/frontend/package-lock.json")
}

tasks.register('installFrontendDeps', com.github.gradle.node.npm.task.NpmTask) {
    dependsOn cleanFrontend
    npmCommand = ['install']
    args = ['--force']
    workingDir = file("${project.projectDir}/frontend")
    
    // Show npm output
    execOverrides {
        it.standardOutput = System.out
        it.errorOutput = System.err
    }
}

tasks.register('buildFrontend', com.github.gradle.node.npm.task.NpmTask) {
    dependsOn installFrontendDeps
    npmCommand = ['run', 'build']
    args = []
    workingDir = file("${project.projectDir}/frontend")
    
    // Show npm output
    execOverrides {
        it.standardOutput = System.out
        it.errorOutput = System.err
    }
}

processResources.dependsOn buildFrontend

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

test {
    useJUnitPlatform() 
}